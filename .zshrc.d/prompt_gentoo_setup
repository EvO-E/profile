# gentoo prompt theme
# customized by Stephan Sokolow for git branch display based on:
# http://www.jukie.net/~bart/blog/20071219221358
# http://www.simplisticcomplexity.com/2008/03/13/show-your-git-branch-name-in-your-prompt/#comment-4166

prompt_gentoo_help () {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt gentoo [<promptcolor> [<usercolor> [<rootcolor>]]]

EOF
}

function parse_git_branch() {
	ref=$(git symbolic-ref HEAD 2> /dev/null) || return
	echo " [${ref#refs/heads/}]"
}

prompt_gentoo_precmd() {
  git_branch_prompt="$(parse_git_branch)"
  path_prompt="%B%F{$prompt_gentoo_prompt}%1~$git_branch_prompt"
  PS1="$base_prompt$path_prompt %# $post_prompt"
  PS2="$base_prompt$path_prompt %_> $post_prompt"
  PS3="$base_prompt$path_prompt ?# $post_prompt"
}

prompt_gentoo_setup () {
  prompt_gentoo_prompt=${1:-'blue'}
  prompt_gentoo_user=${2:-'green'}
  prompt_gentoo_root=${3:-'red'}

  if [ "$USER" = 'root' ]
  then
    base_prompt="%B%F{$prompt_gentoo_root}%m%k "
  else
    base_prompt="%B%F{$prompt_gentoo_user}%n@%m%k "
  fi
  post_prompt="%b%f%k"

  #setopt noxtrace localoptions
  precmd_functions+='prompt_gentoo_precmd'
}

prompt_gentoo_setup "$@"
